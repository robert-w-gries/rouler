int = @{ sign? ~ uint }
  sign = @{ "+" | "-" }
  uint = @{ ASCII_DIGIT+ }

op = _{ plus | minus | times | slash }
  plus = { "+" }
  minus = { "-" }
  times = { "*" }
  slash = { "/" }

roll = ${ die ~ take? ~ target_roll? }

die = ${ count? ~ ^"d" ~ die_type }
count = @{ uint }
die_type = _{ custom_die | normal_die }
  custom_die = !{ "[" ~ int ~ ("," ~ int )* ~ "]" }
  normal_die = @{ uint }

take = _{ (keep | drop) ~ uint }
  drop = @{ ^"dl" | ^"d" }
  keep = @{ ^"kh" | ^"k" }

target_roll = _{ (gte | gt | lte | lt | eq) ~ uint }
  gt = @{ ">" }
  gte = @{ ">=" }
  lt = @{ "<" }
  lte = @{ "<=" }
  eq = @{ "==" }

expr = { term ~ (op ~ term)* }
term = _{ roll | int | "(" ~ expr ~ ")" }

roll_cmd = _{ "/roll" | "/r" }
calc = _{ SOI ~ roll_cmd? ~ expr ~ ANY* ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

int = @{ sign? ~ uint }
  sign = { "+" | "-" }
  uint = @{ ASCII_DIGIT+ }

op = _{ plus | minus | times | slash }
  plus = { "+" }
  minus = { "-" }
  times = { "*" }
  slash = { "/" }

custom_die = !{ "[" ~ int ~ ("," ~ int )* ~ "]" }
drop = ${ ^"dl" | ^"d" }
keep = ${ ^"kh" | ^"k" }
roll = ${ uint ~ ^"d" ~ (custom_die | uint) ~ ((drop | keep) ~ uint)? }

expr = { term ~ (op ~ term)* ~ ANY* }
term = _{ roll | int | "(" ~ expr ~ ")" }

roll_cmd = _{ "/roll" | "/r" }
calc = _{ SOI ~ roll_cmd? ~ expr ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

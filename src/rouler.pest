int = @{ sign? ~ uint }
  sign = { "+" | "-" }
  uint = @{ ASCII_DIGIT+ }

op = _{ plus | minus | times | slash }
  plus = { "+" }
  minus = { "-" }
  times = { "*" }
  slash = { "/" }

roll = ${ num_rolls ~ ^"d" ~ die_type ~ take? ~ target_roll? }

num_rolls = @{ uint }

die_type = _{ custom_die | normal_die }
  custom_die = !{ "[" ~ int ~ ("," ~ int )* ~ "]" }
  normal_die = @{ uint }

take = _{ (keep | drop) ~ uint }
  drop = @{ ^"dl" | ^"d" }
  keep = @{ ^"kh" | ^"k" }

target_roll = _{ (gte | gt | lte | lt) ~ uint }
  gt = @{ ">" }
  gte = @{ ">=" }
  lt = @{ "<" }
  lte = @{ "<=" }

expr = { term ~ (op ~ term)* ~ ANY* }
term = _{ roll | int | "(" ~ expr ~ ")" }

roll_cmd = _{ "/roll" | "/r" }
calc = _{ SOI ~ roll_cmd? ~ expr ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
